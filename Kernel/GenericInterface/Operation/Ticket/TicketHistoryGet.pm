# --
# Copyright (C) 2001-2020 OTRS AG, https://otrs.com/
# --
# This software comes with ABSOLUTELY NO WARRANTY. For details, see
# the enclosed file COPYING for license information (GPL). If you
# did not receive this file, see https://www.gnu.org/licenses/gpl-3.0.txt.
# --

package Kernel::GenericInterface::Operation::Ticket::TicketHistoryGet;

use strict;
use warnings;

use Kernel::System::VariableCheck qw(IsArrayRefWithData IsHashRefWithData IsStringWithData);

use parent qw(
    Kernel::GenericInterface::Operation::Common
    Kernel::GenericInterface::Operation::Ticket::Common
);

our $ObjectManagerDisabled = 1;

=head1 NAME

Kernel::GenericInterface::Operation::Ticket::TicketHistoryGet - GenericInterface Ticket History Get Operation backend

=head1 PUBLIC INTERFACE

=head2 new()

usually, you want to create an instance of this
by using Kernel::GenericInterface::Operation->new();

=cut

sub new {
    my ( $Type, %Param ) = @_;

    my $Self = {};
    bless( $Self, $Type );

    # check needed objects
    for my $Needed (qw(DebuggerObject WebserviceID)) {
        if ( !$Param{$Needed} ) {
            return {
                Success      => 0,
                ErrorMessage => "Got no $Needed!",
            };
        }

        $Self->{$Needed} = $Param{$Needed};
    }

    return $Self;
}

=head2 Run()

perform TicketHistoryGet Operation. This function is able to return
one or more ticket entries in one call.

    my $Result = $OperationObject->Run(
        Data => {
            UserLogin            => 'some agent login',                            # UserLogin or SessionID is required
            SessionID            => 123,
            Password             => 'some password',                               # if UserLogin is sent then Password is required
            TicketID             => '32,33',                                       # required, could be coma separated IDs or an Array
        },
    );

    $Result = {
        Success      => 1,                                # 0 or 1
        ErrorMessage => '',                               # In case of an error
        Data         => {
            TicketHistory => [
                {
                    TicketID => 123,
                    History  => [
                        # ...
                    ],
                },
            ],
        },
    };

=cut

=pod
@api {post} /ticket/history/get Get a ticket history.
@apiName Get History
@apiGroup Ticket
@apiVersion 1.0.0

@apiExample Example usage:
  {
    "SessionID": "a0uShqmDGXkiSyPRjmFnPH2vRH4yPc8J",
    "TicketID": "3,4"
  }

@apiParam (Request body) {String} [UserLogin] User login to create sesssion.
@apiParam (Request body) {String} [Password] Password to create session.
@apiParam (Request body) {String} SessionID session id generated by session create method.
@apiParam (Request body) {String} TicketID Ticket IDs separated by comma.

@apiErrorExample {json} Error example:
  HTTP/1.1 200 Success
  {
    "Error": {
      "ErrorCode": "TicketHistoryGet.AuthFail",
      "ErrorMessage": "TicketHistoryGet: Authorization failing!"
    }
  }
@apiSuccessExample {json} Success example:
  HTTP/1.1 200 Success
  {
    "TicketHistory": [
      {
        "History": [
          {
            "OwnerID": 1,
            "HistoryTypeID": 21,
            "PriorityID": 1,
            "StateID": 1,
            "CreateBy": 1,
            "CreateTime": "2021-05-29 19:23:59",
            "ArticleID": 0,
            "HistoryType": "CustomerUpdate",
            "TypeID": 1,
            "TicketID": "3",
            "QueueID": 1,
            "Name": "%%CustomerID=LIGEROSMART;CustomerUser=ricardo.silva;"
          },
          {
            "OwnerID": 1,
            "HistoryTypeID": 28,
            "PriorityID": 1,
            "StateID": 1,
            "CreateBy": 1,
            "CreateTime": "2021-05-29 19:23:59",
            "ArticleID": 0,
            "HistoryType": "TicketDynamicFieldUpdate",
            "TypeID": 1,
            "TicketID": "3",
            "Name": "%%FieldName%%TicketKey%%Value%%eccbc87e4b5ce2fe28308fd9f2a7baf3%%OldValue%%",
            "QueueID": 1
          },
          {
            "OwnerID": 1,
            "HistoryTypeID": 28,
            "PriorityID": 1,
            "CreateBy": 1,
            "CreateTime": "2021-05-29 19:23:59",
            "StateID": 1,
            "ArticleID": 0,
            "TypeID": 1,
            "HistoryType": "TicketDynamicFieldUpdate",
            "TicketID": "3",
            "QueueID": 1,
            "Name": "%%FieldName%%TicketKey%%Value%%teste%%OldValue%%eccbc87e4b5ce2fe28308fd9f2a7baf3"
          },
          {
            "PriorityID": 1,
            "OwnerID": 1,
            "HistoryTypeID": 1,
            "QueueID": 1,
            "Name": "Test history",
            "HistoryType": "NewTicket",
            "TypeID": 1,
            "TicketID": "3",
            "StateID": 1,
            "CreateTime": "2021-05-29 19:23:59",
            "CreateBy": 1,
            "ArticleID": 35
          },
          {
            "PriorityID": 1,
            "OwnerID": 1,
            "HistoryTypeID": 25,
            "Name": "Reset of unlock time.",
            "QueueID": 1,
            "HistoryType": "Misc",
            "TypeID": 1,
            "TicketID": "3",
            "StateID": 1,
            "CreateBy": 1,
            "CreateTime": "2021-05-29 19:23:59",
            "ArticleID": 0
          },
          {
            "TicketID": "3",
            "HistoryType": "TimeAccounting",
            "TypeID": 1,
            "Name": "%%1%%0h 0m",
            "QueueID": 1,
            "ArticleID": 35,
            "StateID": 1,
            "CreateTime": "2021-05-29 19:24:00",
            "CreateBy": 1,
            "HistoryTypeID": 20,
            "OwnerID": 1,
            "PriorityID": 1
          },
          {
            "OwnerID": 1,
            "HistoryTypeID": 28,
            "PriorityID": 1,
            "TypeID": 1,
            "HistoryType": "TicketDynamicFieldUpdate",
            "TicketID": "3",
            "QueueID": 1,
            "Name": "%%FieldName%%AccountedTimeInMin%%Value%%0h 0m%%OldValue%%",
            "CreateTime": "2021-05-29 19:24:00",
            "CreateBy": 1,
            "StateID": 1,
            "ArticleID": 0
          },
          {
            "PriorityID": 1,
            "OwnerID": 1,
            "HistoryTypeID": 28,
            "StateID": 1,
            "CreateBy": 1,
            "CreateTime": "2021-05-29 19:24:00",
            "ArticleID": 0,
            "Name": "%%FieldName%%AccountedTime%%Value%%0:00%%OldValue%%",
            "QueueID": 1,
            "HistoryType": "TicketDynamicFieldUpdate",
            "TypeID": 1,
            "TicketID": "3"
          }
        ],
        "TicketID": "3"
      },
      {
        "TicketID": "4",
        "History": [
          {
            "PriorityID": 1,
            "HistoryTypeID": 21,
            "OwnerID": 1,
            "ArticleID": 0,
            "CreateBy": 1,
            "CreateTime": "2021-05-29 19:25:05",
            "StateID": 1,
            "Name": "%%CustomerID=LIGEROSMART;CustomerUser=ricardo.silva;",
            "QueueID": 1,
            "TicketID": "4",
            "TypeID": 1,
            "HistoryType": "CustomerUpdate"
          },
          {
            "StateID": 1,
            "CreateBy": 1,
            "CreateTime": "2021-05-29 19:25:05",
            "ArticleID": 0,
            "Name": "%%FieldName%%TicketKey%%Value%%a87ff679a2f3e71d9181a67b7542122c%%OldValue%%",
            "QueueID": 1,
            "HistoryType": "TicketDynamicFieldUpdate",
            "TypeID": 1,
            "TicketID": "4",
            "PriorityID": 1,
            "OwnerID": 1,
            "HistoryTypeID": 28
          },
          {
            "TypeID": 1,
            "HistoryType": "TicketDynamicFieldUpdate",
            "TicketID": "4",
            "Name": "%%FieldName%%TicketKey%%Value%%teste%%OldValue%%a87ff679a2f3e71d9181a67b7542122c",
            "QueueID": 1,
            "CreateTime": "2021-05-29 19:25:05",
            "CreateBy": 1,
            "StateID": 1,
            "ArticleID": 0,
            "OwnerID": 1,
            "HistoryTypeID": 28,
            "PriorityID": 1
          },
          {
            "PriorityID": 1,
            "HistoryTypeID": 1,
            "OwnerID": 1,
            "QueueID": 1,
            "Name": "Test history",
            "TicketID": "4",
            "HistoryType": "NewTicket",
            "TypeID": 1,
            "ArticleID": 36,
            "StateID": 1,
            "CreateBy": 1,
            "CreateTime": "2021-05-29 19:25:05"
          },
          {
            "HistoryTypeID": 25,
            "OwnerID": 1,
            "PriorityID": 1,
            "ArticleID": 0,
            "StateID": 1,
            "CreateBy": 1,
            "CreateTime": "2021-05-29 19:25:05",
            "TicketID": "4",
            "HistoryType": "Misc",
            "TypeID": 1,
            "Name": "Reset of unlock time.",
            "QueueID": 1
          },
          {
            "PriorityID": 1,
            "HistoryTypeID": 20,
            "OwnerID": 1,
            "ArticleID": 36,
            "CreateBy": 1,
            "CreateTime": "2021-05-29 19:25:05",
            "StateID": 1,
            "QueueID": 1,
            "Name": "%%1%%0h 0m",
            "TicketID": "4",
            "TypeID": 1,
            "HistoryType": "TimeAccounting"
          },
          {
            "QueueID": 1,
            "Name": "%%FieldName%%AccountedTimeInMin%%Value%%0h 0m%%OldValue%%",
            "TicketID": "4",
            "TypeID": 1,
            "HistoryType": "TicketDynamicFieldUpdate",
            "ArticleID": 0,
            "CreateBy": 1,
            "CreateTime": "2021-05-29 19:25:06",
            "StateID": 1,
            "PriorityID": 1,
            "HistoryTypeID": 28,
            "OwnerID": 1
          },
          {
            "PriorityID": 1,
            "HistoryTypeID": 28,
            "OwnerID": 1,
            "Name": "%%FieldName%%AccountedTime%%Value%%0:00%%OldValue%%",
            "QueueID": 1,
            "TicketID": "4",
            "TypeID": 1,
            "HistoryType": "TicketDynamicFieldUpdate",
            "ArticleID": 0,
            "CreateBy": 1,
            "CreateTime": "2021-05-29 19:25:06",
            "StateID": 1
          }
        ]
      }
    ]
  }
=cut
sub Run {
    my ( $Self, %Param ) = @_;

    my $Result = $Self->Init(
        WebserviceID => $Self->{WebserviceID},
    );

    if ( !$Result->{Success} ) {
        return $Self->ReturnError(
            ErrorCode    => 'Webservice.InvalidConfiguration',
            ErrorMessage => $Result->{ErrorMessage},
        );
    }

    my ( $UserID, $UserType ) = $Self->Auth(
        %Param,
    );

    if ( !$UserID ) {
        return $Self->ReturnError(
            ErrorCode    => 'TicketHistoryGet.AuthFail',
            ErrorMessage => "TicketHistoryGet: Authorization failing!",
        );
    }
    if ( $UserType ne 'User' ) {
        return $Self->ReturnError(
            ErrorCode    => 'TicketHistoryGet.AuthFail',
            ErrorMessage => "TicketHistoryGet: User needs to an Agent!",
        );
    }

    for my $Needed (qw(TicketID)) {
        if ( !$Param{Data}->{$Needed} ) {
            return $Self->ReturnError(
                ErrorCode    => 'TicketHistoryGet.MissingParameter',
                ErrorMessage => "TicketHistoryGet: $Needed parameter is missing!",
            );
        }
    }
    my $ErrorMessage = '';

    # All needed variables.
    my @TicketIDs;
    if ( IsStringWithData( $Param{Data}->{TicketID} ) ) {
        @TicketIDs = split( /,/, $Param{Data}->{TicketID} );
    }
    elsif ( IsArrayRefWithData( $Param{Data}->{TicketID} ) ) {
        @TicketIDs = @{ $Param{Data}->{TicketID} };
    }
    else {
        return $Self->ReturnError(
            ErrorCode    => 'TicketHistoryGet.WrongStructure',
            ErrorMessage => "TicketHistoryGet: Structure for TicketID is not correct!",
        );
    }

    TICKET:
    for my $TicketID (@TicketIDs) {

        my $Access = $Self->CheckAccessPermissions(
            TicketID => $TicketID,
            UserID   => $UserID,
            UserType => $UserType,
        );

        next TICKET if $Access;

        return $Self->ReturnError(
            ErrorCode    => 'TicketHistoryGet.AccessDenied',
            ErrorMessage => "TicketHistoryGet: User does not have access to the ticket $TicketID!",
        );
    }

    my $ReturnData = {
        Success => 1,
    };

    my @Histories;

    my %SafeAttributes = (
        TicketID      => 1,
        ArticleID     => 1,
        Name          => 1,
        CreateBy      => 1,
        CreateTime    => 1,
        HistoryType   => 1,
        QueueID       => 1,
        OwnerID       => 1,
        PriorityID    => 1,
        StateID       => 1,
        HistoryTypeID => 1,
        TypeID        => 1,
    );

    # start ticket loop
    TICKET:
    for my $TicketID (@TicketIDs) {

        # get ticket object
        my $TicketObject = $Kernel::OM->Get('Kernel::System::Ticket');

        my @LinesRaw = $TicketObject->HistoryGet(
            TicketID => $TicketID,
            UserID   => $UserID,
        );

        my @Lines;
        for my $Line (@LinesRaw) {

            my %Attributes;

            ATTRIBUTE:
            for my $Attribute ( sort keys %{$Line} ) {
                next ATTRIBUTE if !$SafeAttributes{$Attribute};

                $Attributes{$Attribute} = $Line->{$Attribute};
            }

            push @Lines, \%Attributes;
        }

        push @Histories, {
            TicketID => $TicketID,
            History  => \@Lines,
        };
    }

    if ( !scalar @Histories ) {
        $ErrorMessage = 'Could not get Ticket history data'
            . ' in Kernel::GenericInterface::Operation::Ticket::TicketHistoryGet::Run()';

        return $Self->ReturnError(
            ErrorCode    => 'TicketHistoryGet.NotTicketData',
            ErrorMessage => "TicketHistoryGet: $ErrorMessage",
        );
    }

    # set ticket data into return structure
    $ReturnData->{Data}->{TicketHistory} = \@Histories;

    # return result
    return $ReturnData;
}

1;

=head1 TERMS AND CONDITIONS

This software comes with ABSOLUTELY NO WARRANTY. For details, see
the enclosed file COPYING for license information (GPL). If you
did not receive this file, see L<https://www.gnu.org/licenses/gpl-3.0.txt>.

=cut
